<?xml version="1.0" encoding="utf-8"?>
<!-- iyuncai.com uploader -->
<s:Application
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:s="library://ns.adobe.com/flex/spark"

    click="clickHandler(event)"
    applicationComplete="applicationCompleteHandler()"
    backgroundAlpha="0">
    <s:layout>
        <s:VerticalLayout/>
    </s:layout>
    <!-- Coding here -->
	<fx:Script>
	<![CDATA[
        private var infoText:String;
        private var fileFilterDescription:String;
        private var fileFilterExtension:String;
        private var uploadUrl:String;
        private var onUpload:String;
        private var imageWidth:Number = 0;
        private var imageHeight:Number = 0;
        private var w:Number = 0;
        private var h:Number = 0;

        private var fileRef:FileReference;
        private var allFilter:FileFilter = new FileFilter("All", "*");

        private function onFile(e:Event):void {
            fileRef.addEventListener(Event.COMPLETE, onLoad);
            fileRef.load();
        }
        private function onLoad(e:Event):void {
            fileRef.removeEventListener(Event.COMPLETE, onLoad);

            var ldr:Loader = new Loader();
            ldr.contentLoaderInfo.addEventListener(Event.COMPLETE, onImage);
            ldr.loadBytes(fileRef.data);
        }
        private function onImage(e:Event):void {
            var bitmap:Bitmap = e.currentTarget.loader.content as Bitmap;
            var pass:Boolean = true;
            if (imageWidth!=0) {
                if (bitmap.width != imageWidth) {
                    pass = false;
                }
            }
            if (imageHeight!=0) {
                if (bitmap.height != imageHeight) {
                    pass = false;
                }
            }

            if (pass) {
                w = bitmap.width;
                h = bitmap.height;
                fileRef.addEventListener(Event.COMPLETE, onUploadComplete);
                var url:URLRequest = new URLRequest(uploadUrl);
                fileRef.upload(url);
            }else{
                if (onUpload && ExternalInterface.available) {
                    ExternalInterface.call(onUpload, false, '图片必须为' + imageWidth + 'x' + imageHeight + '');
                }
            }
        }
        private function onUploadProgress(e:ProgressEvent):void {
            var percent:Number = Math.round(e.bytesLoaded*100/e.bytesTotal);
            info.text = percent + "%";
        }
        private function onUploadComplete(e:Event):void {
            info.text = infoText;
        }
        private function onResponse(e:DataEvent):void {
            var result:XML = new XML(e.data);
            var response:String = result.toString();
            if (onUpload && ExternalInterface.available) {
                ExternalInterface.call(onUpload, true, response, w, h);
            }
        }
        private function onError(e:IOErrorEvent):void {
            info.text = "Error";
            trace(e);
        }
        private function browseFile():void {
            fileRef = new FileReference();

            fileRef.addEventListener(Event.SELECT, onFile);
            fileRef.addEventListener(ProgressEvent.PROGRESS, onUploadProgress);
            fileRef.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA, onResponse);

            fileRef.addEventListener(IOErrorEvent.IO_ERROR, onError);

            if (fileFilterDescription && fileFilterExtension) {
                var fileFilter:FileFilter = new FileFilter(fileFilterDescription, fileFilterExtension);
                fileRef.browse([fileFilter, allFilter]);
            }else{
                fileRef.browse([allFilter]);
            }
        }

        private function clickHandler(e:MouseEvent):void {
            browseFile();
        }

        public function applicationCompleteHandler():void {
            infoText = parameters.infoText;
            if (infoText==null) {
                infoText = 'Upload...'
            }
            info.text = infoText;

            fileFilterDescription = parameters.filterDesc;
            fileFilterExtension = parameters.filterExt;
            uploadUrl = parameters.uploadUrl;
            onUpload = parameters.onUpload;

            imageWidth = Number(parameters.imageWidth);
            imageHeight = Number(parameters.imageHeight);
        }
	]]>
	</fx:Script>

    <s:SkinnableContainer width="100%" height="100%" skinClass="RoundedCornerSkin">
        <s:Group width="100%" height="100%" verticalCenter="0" horizontalCenter="0">
            <s:Label id="info" text='Upload...' fontSize="14" color="black" baseline="16" width="100%" textAlign="center" fontFamily="sans-serif"/>
        </s:Group>
    </s:SkinnableContainer>
</s:Application>
